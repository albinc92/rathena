//===== rAthena Script =======================================
//= Enchantress NPC
//===== By: ==================================================
//= albinc92
//===== Current Version: =====================================
//= 1.0a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Apply random magic options upon unenchanted gear.
//============================================================

roy_in02,44,113,4	script	Enchantress	916,{
	set .@cost, 10000;
	set .@item, 7597;
	set .@amnt, 3;
	setarray .@weaponid[0],
			3, 4, 5, 6, 7, 8,
			13, 14, 16, 17, 18, 19, 24,
			147, 148, 151, 152,
			164, 166,
			168, 170, 171, 172;
	setarray .@weaponbase[0],
			1, 1, 1, 1, 1, 1,
			5, 5, 10, 25, 25, 25, 10,
			5, 5, 5, 5,
			10, 5,
			10, 10, 3, 10;
	setarray .@weaponbonus[0],
			2, 2, 2, 2, 2, 2,
			5, 5, 15, 25, 75, 25, 15,
			5, 5, 5, 5,
			10, 5,
			15, 15, 4, 15;
	setarray .@armorid[0],
			3, 4, 5, 6, 7, 8,
			9, 10, 11, 12,
			20, 21, 22, 23,
			165, 167, 171;
	setarray .@armorbase[0],
			1, 1, 1, 1, 1, 1,
			5, 5, 10, 10,
			1, 1, 5, 3,
			5, 5, 3;
	setarray .@armorbonus[0],
			2, 2, 2, 2, 2, 2,
			5, 5, 10, 10,
			2, 2, 6, 3,
			5, 5, 4;

	disable_items;

	cutin "ra_usti1.bmp",2;
	mes "["+strnpcinfo(1)+"]";
	mes "Salutations, have you come to employ my services?";

L_Menu:
	// Menu
	switch(select("Enchant item:Disenchant item:Information:Cancel")) {

	// Enchant Item
L_Enchant:
	case 1:
		// Get equipment array
		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for(.@i = 1; .@i < getarraysize(.@indices); ++.@i) {
			if(getequipisequiped(.@indices[.@i])) {
				if(!getequiprandomoption(.@indices[.@i], 0, ROA_ID)) {
					.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
					.@equipped = 1;
				}
			}
			.@menu$ = .@menu$ + ":";
		}

		// Check if equipped at all
		// Check if item already has random options
		if (.@equipped == 0) {
			next;
			mes "["+strnpcinfo(1)+"]";
			mes "Ehm. You are not wearing anything that is enchantable. Maybe you would like me to disenchant something instead?";
			
			switch(select("Disenchant item:Cancel")) {
			case 1:
				goto L_Disenchant;
			case 2:
				goto L_End;
			}
		}

		next;
		mes "["+strnpcinfo(1)+"]";
		mes "Very well. Which item would you like me to enchant?";

		.@part = .@indices[select(.@menu$)];

		next;
		mes "["+strnpcinfo(1)+"]";
		mes "The enchantment process will cost you ^ff0000" + .@amnt + " " + getitemname(.@item) +"^000000 and ^ff0000" + .@cost + "z^000000. Is this okay?";
		
		switch(select("Yes:Cancel")) {
		case 2:
			goto L_End;
		}
		next;

		if(zeny < .@cost) {
			mes "["+strnpcinfo(1)+"]";
			mes "You don't have enough zeny I'm afraid.";
			close2;
			cutin "", 255;
			end;
		}

		if(countitem(.@item) < .@amnt) {
			mes "["+strnpcinfo(1)+"]";
			mes "You do not have ^ff0000" + .@amnt + " " + getitemname(.@item) + "^000000. Come back later.";
			close2;
			cutin "", 255;
			end;
		}

		mes "["+strnpcinfo(1)+"]";
		mes "Excellent! I will now commence the enchanting process.";
		next;
		
		mes "["+strnpcinfo(1)+"]";
		mes "*mumble mumble*";
		specialeffect EF_BEGINSPELL6;
		progressbar_npc "#00FF00",1;
		specialeffect2 65;

		delitem .@item, .@amnt;
		set zeny, zeny - .@cost;

		if(getiteminfo(getequipid(.@part), 2) == 9) {
			set .@index, rand(getarraysize(.@weaponid));
			setrandomoption(.@part,0,.@weaponid[.@index], (.@weaponbase[.@index] + rand(0, .@weaponbonus[.@index])), 0);
		} else {
			set .@index, rand(getarraysize(.@armorid));
			setrandomoption(.@part,0,.@armorid[.@index], (.@armorbase[.@index] + rand(0, .@armorbonus[.@index])), 0);
		}

		next;
		emotion ET_BEST;
		mes "["+strnpcinfo(1)+"]";
		mes "The process was successful!";
		goto L_End;

	// Disenchant item
L_Disenchant:
	case 2:
		// Get equipment array
		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for(.@i = 1; .@i < getarraysize(.@indices); ++.@i) {
			if(getequipisequiped(.@indices[.@i])) {
				if(getequiprandomoption(.@indices[.@i], 0, ROA_ID)) {
					.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
					.@equipped = 1;
				}
			}
			.@menu$ = .@menu$ + ":";
		}

		// Check if equipped at all
		// Check if item already has random options
		if (.@equipped == 0) {
			next;
			mes "["+strnpcinfo(1)+"]";
			mes "Ehm. You are not wearing anything that is disenchantable. Maybe you would like me to enchant something instead?";
			
			switch(select("Enchant item:Cancel")) {
			case 1:
				goto L_Enchant;
			case 2:
				goto L_End;
			}
		}

		next;
		mes "["+strnpcinfo(1)+"]";
		mes "Very well. Which item would you like me to enchant?";

		.@part = .@indices[select(.@menu$)];

		// Calculate no of random options on item
		set .@nbr, 1;
		for (set .@i, 1; .@i < MAX_ITEM_RDM_OPT; .@i++) {
			if(getequiprandomoption(.@part, .@i, ROA_ID)) {
				set .@nbr, .@nbr + 1;
			}
		}

		next;
		mes "["+strnpcinfo(1)+"]";
		mes "I see the selected item has ^ff0000" + .@nbr + " Magical Affixes^000000 attached to it.";

		next;
		mes "["+strnpcinfo(1)+"]";
		mes "The disenchantment process will thus cost you ^ff0000" + ((.@cost / 4) * .@nbr) + "z^000000 and yield ^ff0000" + .@nbr + " " + getitemname(.@item) + "(s)^000000. Is this okay?";
		
		switch(select("Yes:Cancel")) {
		case 2:
			goto L_End;
		}
		next;

		if(zeny < ((.@cost / 4) * .@nbr)) {
			mes "["+strnpcinfo(1)+"]";
			mes "You don't have enough zeny I'm afraid.";
			close2;
			cutin "", 255;
			end;
		}

		mes "["+strnpcinfo(1)+"]";
		mes "Excellent! I will now commence the disenchanting process.";
		next;
		
		mes "["+strnpcinfo(1)+"]";
		mes "*mumble mumble*";
		specialeffect EF_BEGINSPELL6;
		progressbar_npc "#00FF00",1;
		specialeffect2 234;

		getitem .@item, .@nbr;
		set zeny, zeny - ((.@cost / 4) * .@nbr);

		.@equip_id = getequipid(.@part);
		.@refine = getequiprefinerycnt(.@part);
		for (set .@i, 0; .@i < 4; .@i++) {
			.@card[.@i] = getequipcardid(.@part, .@i);
		}
		delequip .@part;
		getitem2 .@equip_id, 1, 1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];

		next;
		emotion ET_BEST;
		mes "["+strnpcinfo(1)+"]";
		mes "The process was successful!";
		goto L_End;

	// Information
	case 3:
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "I have acquired the knowledge of harnessing the magical properties bound within equipment items.";
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "For a small fee I can either bind ^ff00001 Random Magical Affix^000000 unto your equipment, or ^ff0000remove any affixes^000000.";
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "Amazing, right?";
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "In order to enchant an item, I require ^ff0000" + .@amnt + "x " + getitemname(.@item) + "^000000 as a conduit.";
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "You can acquire ^ff0000" + getitemname(.@item) + "s^000000 by Disenchanting other enchanted items.";
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "Neat, huh?";
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "You can take a look at the Signpost to my right for more information of available magical affixes.";
		next;
		mes "["+strnpcinfo(1)+"]";
		mes "Now, what action would you like me to perform today?";
		goto L_Menu;
	}

L_End:
	next;
	mes "["+strnpcinfo(1)+"]";
	mes "Be seeing you.";
	close2;
	cutin "", 255;
	end;
}

// Signpost
roy_in02,46,110,3	script	List of Affixes	837,{

	mes "["+strnpcinfo(1)+"]";
	mes "This signpost seems to list available magical affixes for weapons and armor. What type would you like to preview?";
	
L_Browse:
	switch(select("Weapon affixes:Armor affixes:Cancel")) {
	case 1:
		next;
		mes "[^ff0000Weapon Affixes I^000000]";
		mes "STR +(^ff00001 - 3^000000)";
		mes "AGI +(^ff00001 - 3^000000)";
		mes "VIT +(^ff00001 - 3^000000)";
		mes "INT +(^ff00001 - 3^000000)";
		mes "DEX +(^ff00001 - 3^000000)";
		mes "LUK +(^ff00001 - 3^000000)";
		next;
		mes "[^ff0000Weapon Affixes II^000000]";
		mes "ATK +(^ff00005 - 10^000000)%";
		mes "MATK +(^ff00005 - 10^000000)%";
		mes "ATK +(^ff000025 - 50^000000)";
		mes "MATK +(^ff000025 - 50^000000)";
		mes "ASPD +(^ff000010 - 25^000000)%";
		mes "HIT +(^ff000025 - 100^000000)";
		mes "CRIT +(^ff000010 - 25^000000)%";
		next;
		mes "[^ff0000Weapon Affixes III^000000]";
		mes "Damage vs. Normal Monsters +(^ff00005 - 10^000000)%";
		mes "Damage vs. Boss Monsters +(^ff00005 - 10^000000)%";
		mes "Magic Damage vs. Normal Monsters +(^ff00005 - 10^000000)%";
		mes "Magic Damage vs. Boss Monsters +(^ff00005 - 10^000000)%";
		next;
		mes "[^ff0000Weapon Affixes IV^000000]";
		mes "Critical Damage +(^ff000010 - 20^000000)%";
		mes "Ranged Attack Damage +(^ff00005 - 10^000000)%";
		next;
		mes "[^ff0000Weapon Affixes V^000000]";
		mes "Heal Power +(^ff000010 - 25^000000)%";
		mes "Cast Rate -(^ff000010 - 25^000000)%";
		mes "After-Cast Delay -(^ff00003 - 7^000000)%";
		mes "SP Consumption -(^ff000010 - 25^000000)%";
		next;
		goto L_Browse;
	case 2:
		next;
		mes "[^ff0000Armor Affixes I^000000]";
		mes "STR +(^ff00001 - 3^000000)";
		mes "AGI +(^ff00001 - 3^000000)";
		mes "VIT +(^ff00001 - 3^000000)";
		mes "INT +(^ff00001 - 3^000000)";
		mes "DEX +(^ff00001 - 3^000000)";
		mes "LUK +(^ff00001 - 3^000000)";
		next;
		mes "[^ff0000Armor Affixes II^000000]";
		mes "Max HP +(^ff00005 - 10^000000)%";
		mes "Max SP +(^ff00005 - 10^000000)%";
		mes "HP Regeneration +(^ff000010 - 20^000000)%";
		mes "SP Regeneration +(^ff000010 - 20^000000)%";
		next;
		mes "[^ff0000Armor Affixes III^000000]";
		mes "DEF +(^ff00001 - 3^000000)";
		mes "MDEF +(^ff00001 - 3^000000)";
		mes "Flee +(^ff00005 - 11^000000)";
		mes "Perfect Dodge +(^ff00003 - 6^000000)";
		next;
		mes "[^ff0000Armor Affixes IV^000000]";
		mes "Critical Damage Resist +(^ff00005 - 10^000000)%";
		mes "Ranged Damage Resist +(^ff00005 - 10^000000)%";
		next;
		mes "[^ff0000Armor Affixes IV^000000]";
		mes "After-Cast Delay -(^ff00003 - 7^000000)%";
		goto L_Browse;
	}
	
L_End:
	close2;
	cutin "", 255;
	end;
}
