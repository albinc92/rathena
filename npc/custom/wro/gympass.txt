//===== rAthena Script =======================================
//= Gym Pass Cash Item NPC
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: =================================
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================

royal,194,57,7	script	Galtun the Giant#GymPass	492,{
	mes "[Galtun]";
	mes "Being strooong is noot that haard.";
	next;
	mes "[Galtun]";
	mes "If yoou shoow mee ^FF0000Gym Paass^000000, I can teaach youu to be stroong toooo!";
	next;
	mes "[Galtun]";
	mes "You noormans are puuuny however... Can oonly train with Galtuun littlee times.";
	next;
	mes "[Galtun]";
	mes "I think youu caan doo ^FF000010 timees^000000. Aree you up foor it?";
	next;
	switch(select("Yes:No:Um, my workouts wore off.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[Galtun]";
				mes "Heree we goo...";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[Galtun]";
				mes "Aaall done. Not toooo bad for a puuuny nooorman. Keeep this up ^FF00000" + .@remain_carry + "^000000 moore times aand youu be strooong normaan!";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,SKILL_PERM_GRANT;
				close;
			}
			else {
				mes "[Galtun]";
				mes "I see nooo ^FF0000Gyyym Pass^000000...";
				close;
			}
		}
		else {
			mes "[Galtun]";
			mes "Youu noo leaarn moore stroong froom Galtuun! Youu stroong normaan now.";
			close;
		}
	case 2:
		mes "[Galtun]";
		mes "Byee puuny noorman.";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[Galtun]";
			mes "Ooh! Need to keep eaaating to keep strooong bodyy! Eaat heeere!";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[Galtun]";
			mes "Noow all stroong again! Byye normaan.";
			skill "ALL_INCCARRY",gympassmemory,SKILL_PERM_GRANT;
			close;
		}
		else {
			mes "[Galtun]";
			mes "Galtuun is confuuused...";
			close;
		}
	}
}
