//
// Gain bonuses to exp by killing monsters in succession before the timer runs
// out.
//
-	script	mobbing	-1,{

OnPCDieEvent:
OnPCLoadMapEvent:
	if(!getmapflag(strcharinfo(3),mf_pvp)) {
		set mobCombo, 0;
		deltimer strnpcinfo(0)+"::OnComboEnd";
	}
	end;

OnNPCKillEvent:
	if(!getmapflag(strcharinfo(3),mf_pvp)) {
		getmapxy( .@map$, .@x, .@y, BL_PC );
		set mobCombo, mobCombo + 1;
		deltimer strnpcinfo(0)+"::OnComboEnd";
		addtimer 5000,strnpcinfo(0)+"::OnComboEnd";
	}
	end;

OnComboEnd:
	getmapxy( .@map$, .@x, .@y, BL_PC );
	if(mobCombo == 2) {
		mapannounce .@map$,strcharinfo(0) + " just got a DOUBLE KILL COMBO! (" + mobCombo + ")",bc_blue;
		soundeffectall "double_kill.wav", 0, .@map$;
	} else if(mobCombo == 3) {
		mapannounce .@map$,strcharinfo(0) + " just got a MULTI KILL COMBO! (" + mobCombo + ")",bc_blue;
		soundeffectall "multikill.wav", 0, .@map$;
	} else if(mobCombo == 4) {
		mapannounce .@map$,strcharinfo(0) + " just got a MEGA KILL COMBO! (" + mobCombo + ")",bc_blue;
		soundeffectall "megakill.wav", 0, .@map$;
	} else if(mobCombo == 5) {
		mapannounce .@map$,strcharinfo(0) + " just got a ULTRA KILL COMBO! (" + mobCombo + ")",bc_blue;
		soundeffectall "ultrakill.wav", 0, .@map$;
	} else if(mobCombo >= 6) {
		mapannounce .@map$,strcharinfo(0) + " just got a MONSTER KILL COMBO! (" + mobCombo + ")",bc_blue;
		soundeffectall "Monsterkill_F.wav", 0, .@map$;
	}

	set .@bExp, mobCombo * strmobinfo(6, killedrid);
	set .@jExp, mobCombo * strmobinfo(7, killedrid);
	getexp .@bExp,.@jExp;
	specialeffect2 910;
	message strcharinfo(0),"You received bonus experience by killing " + mobCombo + " monsters within a short time-span!";
	set mobCombo, 0;
	end;
}
