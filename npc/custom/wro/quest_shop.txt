//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
roy_in01,181,198,4	script	Supply Master#1	470,{
cutin "3rd_rune_knight",2;
mes "[Supply Master]";
mes "Hail, citizen. I am the supply master, charged with providing necessities to our brave adventurers.";
callfunc "qshop";
}

// Script Core
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Consumables","Miscellaneous","PC Exchange","Glorious Weapons","Valorous Weapons","Brave Weapons","Soldier Weapons","Glorious Armor","Battleground Armor","Cards","Costume Items";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	// Consumables
	Add(1,12208,1,750000,0);
	//Add(1,14533,1,1670000,0);
	//Add(1,14545,1,5000000,0);
	Add(1,12210,1,100000,0);
	Add(1,12214,1,25000,0);
	Add(1,12221,1,100000,0);
	Add(1,12211,1,10000,0);
	Add(1,12031,1,10000,0);
	Add(1,12029,1,10000,0);
	Add(1,12034,1,10000,0);
	Add(1,12030,1,10000,0);
	Add(1,12032,1,10000,0);
	Add(1,12033,1,10000,0);
	Add(1,12028,1,10000,0);
	Add(1,696,1,2000,0);
	Add(1,698,1,2000,0);
	Add(1,688,1,6000,0);
	Add(1,686,1,6000,0);
	Add(1,690,1,6000,0);
	Add(1,700,1,6000,0);
	Add(1,12001,1,6000,0);
	Add(1,694,1,6000,0);
	Add(1,692,1,6000,0);
	Add(1,697,1,10000,0);
	Add(1,699,1,10000,0);
	Add(1,689,1,10000,0);
	Add(1,687,1,10000,0);
	Add(1,691,1,10000,0);
	Add(1,12002,1,10000,0);
	Add(1,695,1,10000,0);
	Add(1,12000,1,10000,0);
	Add(1,693,1,10000,0);
	Add(1,12215,1,10000,0);
	Add(1,12216,1,10000,0);
	Add(1,12246,1,0,0,40112,5);
	Add(1,40006,1,0,0,40112,7);
	Add(1,12103,1,0,0,604,25);

	//Miscellaneous
	Add(2,7776,1,1000000,0);
	Add(2,40005,1,250000,0);
	Add(2,6000,1,1000000,0);
	Add(2,7562,1,125000,0);
	Add(2,6241,1,10000,0,985,5);
	Add(2,16395,1,40000,0,985,20);
	Add(2,16396,1,80000,0,985,30);
	Add(2,17105,1,460000,0,985,100);
	Add(2,6240,1,10000,0,984,5);
	Add(2,16393,1,80000,0,984,20);
	Add(2,16394,1,80000,0,984,30);
	Add(2,17104,1,460000,0,984,100);

	// Poring Coin Exchange
	Add(3,40008,1,10000,0,40001,1);
	Add(3,13807,1,0,0,40001,1);
	Add(3,13804,1,0,0,40001,1);
	Add(3,14548,1,0,0,40001,1);
	Add(3,13805,1,0,0,40001,1);
	Add(3,12132,1,0,0,40001,1);
	Add(3,12260,1,0,0,40001,1);

	// Glorious Weapons
	Add(4,1577,1,0,0,7420,10);
	Add(4,1640,1,0,0,7420,10);
	Add(4,1281,1,0,0,7420,10);
	Add(4,1826,1,0,0,7420,10);
	Add(4,1187,1,0,0,7420,10);
	Add(4,1310,1,0,0,7420,10);
	Add(4,1641,1,0,0,7420,10);
	Add(4,2002,1,0,0,7420,10);
	Add(4,1827,1,0,0,7420,10);
	Add(4,13416,1,0,0,7420,10);
	Add(4,13177,1,0,0,7420,10);
	Add(4,13042,1,0,0,7420,10);
	Add(4,13179,1,0,0,7420,10);
	Add(4,1927,1,0,0,7420,10);
	Add(4,13418,1,0,0,7420,10);
	Add(4,1743,1,0,0,7420,10);
	Add(4,1282,1,0,0,7420,10);
	Add(4,1486,1,0,0,7420,10);
	Add(4,1981,1,0,0,7420,10);
	Add(4,1546,1,0,0,7420,10);
	Add(4,13110,1,0,0,7420,10);
	Add(4,13417,1,0,0,7420,10);
	Add(4,13176,1,0,0,7420,10);
	Add(4,13178,1,0,0,7420,10);
	Add(4,13307,1,0,0,7420,10);
	Add(4,1426,1,0,0,7420,10);
	Add(4,1576,1,0,0,7420,10);
	Add(4,1382,1,0,0,7420,10);

	// Valorous Weapons
	Add(5,13037,1,0,0,7420,10);
	Add(5,1184,1,0,0,7420,10);
	Add(5,1738,1,0,0,7420,10);
	Add(5,1823,1,0,0,7420,10);
	Add(5,1977,1,0,0,7420,10);
	Add(5,1575,1,0,0,7420,10);
	Add(5,1923,1,0,0,7420,10);
	Add(5,1542,1,0,0,7420,10);
	Add(5,1280,1,0,0,7420,10);
	Add(5,13410,1,0,0,7420,10);
	Add(5,13306,1,0,0,7420,10);
	Add(5,1379,1,0,0,7420,10);

	// Brave Weapons
	Add(6,13036,1,0,0,7420,10);
	Add(6,1183,1,0,0,7420,10);
	Add(6,1739,1,0,0,7420,10);
	Add(6,1824,1,0,0,7420,10);
	Add(6,1978,1,0,0,7420,10);
	Add(6,1574,1,0,0,7420,10);
	Add(6,1924,1,0,0,7420,10);
	Add(6,1543,1,0,0,7420,10);
	Add(6,1279,1,0,0,7420,10);
	Add(6,13411,1,0,0,7420,10);
	Add(6,13305,1,0,0,7420,10);
	Add(6,1380,1,0,0,7420,10);

	// Soldier Weapons
	Add(7,13172,1,0,0,7420,10);
	Add(7,13174,1,0,0,7420,10);
	Add(7,13108,1,0,0,7420,10);
	Add(7,13171,1,0,0,7420,10);
	Add(7,13173,1,0,0,7420,10);

	// Glorious Armor
	Add(8,2549,1,0,0,7420,10);
	Add(8,2772,1,0,0,7420,10);
	Add(8,2444,1,0,0,7420,10);
	Add(8,2394,1,0,0,7420,10);
	Add(8,2774,1,0,0,7420,10);
	Add(8,2446,1,0,0,7420,10);
	Add(8,2396,1,0,0,7420,10);
	Add(8,2773,1,0,0,7420,10);
	Add(8,2445,1,0,0,7420,10);
	Add(8,2395,1,0,0,7420,10);

	// Misc PvP Armor
	Add(9,2538,1,0,0,7420,10);
	Add(9,2539,1,0,0,7420,10);
	Add(9,2540,1,0,0,7420,10);
	Add(9,2435,1,0,0,7420,10);
	Add(9,2436,1,0,0,7420,10);
	Add(9,2437,1,0,0,7420,10);
	Add(9,2376,1,0,0,7420,10);
	Add(9,2377,1,0,0,7420,10);
	Add(9,2378,1,0,0,7420,10);
	Add(9,2379,1,0,0,7420,10);
	Add(9,2380,1,0,0,7420,10);
	Add(9,2381,1,0,0,7420,10);
	Add(9,2382,1,0,0,7420,10);
	Add(9,2733,1,0,0,7420,10);
	Add(9,2720,1,0,0,7420,10);
	Add(9,2721,1,0,0,7420,10);
	Add(9,2722,1,0,0,7420,10);
	Add(9,2723,1,0,0,7420,10);
	Add(9,2724,1,0,0,7420,10);
	Add(9,2725,1,0,0,7420,10);

	// Cards
	Add(10,4018,1,10000,0);
	Add(10,4130,1,10000,0);
	Add(10,4059,1,10000,0);

	// Costume Items
	Add(11,40043,1,2500000,0,1045,1,994,100,919,100,2522,50);
	Add(11,40020,1,2500000,0,1636,1,748,100,6041,100,12103,2);
	Add(11,40024,1,2500000,0,7212,2,7218,100,1019,100,7019,5);
	Add(11,40025,1,2500000,0,5090,1,2528,10,949,100,975,5);
	Add(11,40028,1,2500000,0,5116,1,7198,100,513,100,634,5);
	Add(11,40029,1,2500000,0,547,1,7220,100,7345,100,971,25);
	Add(11,40033,1,2500000,0,2659,1,2660,1,2661,1,2662,1,7094,100,7352,100,7095,25);
	Add(11,40035,1,2500000,0,2513,1,7168,100,924,100,602,1000);
	Add(11,40036,1,2500000,0,2319,1,7071,50,7167,100,6020,50);
	Add(11,40037,1,2500000,0,2217,5,990,100,7071,100,2508,1);
	Add(11,40038,1,2500000,0,2677,1,934,100,7220,100,958,50);
	Add(11,40040,1,2500000,0,2255,1,1039,100,919,100,7123,50);
	Add(11,40044,1,2500000,0,10006,1,955,100,928,100,997,50);
	Add(11,40045,1,2500000,0,5045,1,7578,5,1573,10,7063,50);
	Add(11,40046,1,2500000,0,5045,1,7578,5,1573,10,7063,50);
	Add(11,40047,1,2500000,0,2254,1,7442,100,7441,100,7440,50);
	Add(11,40048,1,2500000,0,5096,1,7157,100,998,100,7440,50);
	Add(11,40052,1,2500000,0,4102,1,1001,100,1059,100,5045,5);

	// Donation Rewards
	//Add(12,40015,1,0,25);
	//Add(12,40021,1,0,25);
	//Add(12,40022,1,0,25);
	//Add(12,40030,1,0,25);
	//Add(12,40031,1,0,25);
	//Add(12,40051,1,0,25);
	//Add(12,40039,1,0,25);
	//Add(12,46013,1,0,25);
	//Add(12,40042,1,0,25);
	//Add(12,40049,1,0,25);
	//Add(12,40050,1,0,25);
	//Add(12,40053,1,0,25);
	//Add(12,40054,1,0,25);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+@shop_index,1;
	npcshopattach "qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close2;
				cutin "", 255;
				end;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close2;
				cutin "", 255;
				end;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close2;
			cutin "", 255;
			end;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close2;
			cutin "", 255;
			end;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1
-	shop	qshop7	-1,909:-1
-	shop	qshop8	-1,909:-1
-	shop	qshop9	-1,909:-1
-	shop	qshop10	-1,909:-1
-	shop	qshop11	-1,909:-1
