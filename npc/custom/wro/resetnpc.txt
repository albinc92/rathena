//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================

royal,81,190,6	script	Reset Girl	643,{
	cutin "min01",2;
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	(50*BaseLevel),	(50*BaseLevel),	(90*BaseLevel),	0;
	mes "[Reset Girl]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Sorry you can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life.";
		close2;
		cutin "", 255;
		end;
	}
	mes "Oh hii~~ I can reset your status and skill points. Please select the service you want:";
	//mes "Reset Stats: ^ff0000"+ callfunc("F_InsertComma",.@Reset[1]) +"z^000000";
	//mes "Reset Skills: ^ff0000"+ callfunc("F_InsertComma",.@Reset[0]) +"z^000000";
	//mes "Reset Both: ^ff0000"+ callfunc("F_InsertComma",.@Reset[2]) +"z^000000";
	if(.@Reset[3]) mes "You may only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+", so use "+((.@Reset[3]>1)?"them":"it")+" wisely.";
	next;
	set .@i,(select("Reset Skills (^ff0000" + .@Reset[0] + "z^000000):Reset Stats (^ff0000" + .@Reset[1] + "z^000000):Reset Both (^ff0000" + .@Reset[2] + "z^000000)^000000:Cancel"));
	if(.@i > 3) {
		close2;
		cutin "", 255;
		end;
	}
	mes "[Reset Girl]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "Sorry, you don't have enough Zeny.";
		close2;
		cutin "", 255;
		end;
	}
	if(.@Reset[3]){
		mes "You can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life, are you sure?";
		if(select("Let me think:That's fine") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "There you go!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close2;
	cutin "", 255;
	end;
}
